      SUBROUTINE SOLT_COS
!     EPIC1905
!     THIS SUBPROGRAM ESTIMATES DAILY AVEAGE TEMPERATURE AT THE CENTER
!     OF EACH SOIL LAYER USING COSINE FUNCTION.
      USE PARM
      ! CALCULATE DAMPING DEPTH AS A FUNCTION OF AVERAGE BULK DENSITY AND WATER CONTENT. 
      X2=.5*(TXMX(MO1,ISA)+TXMN(MO1,ISA))   !added by Luca Doro
      ST0X=ST0(ISA)
      F=ABD(ISA)/(ABD(ISA)+686.*EXP(-5.63*ABD(ISA)))
      DP=1.+2.5*F
      WW=.356-.144*ABD(ISA)
      B=LOG(.5/DP)
      WC=.001*SW(ISA)/(WW*Z(LID(NBSL(ISA),ISA),ISA))
      F=EXP(B*((1.-WC)/(1.+WC))**2)
      DD=F*DP
      ! CALCULATE AVE, MAX, & MIN DD 
      ZZ=2.*DD
      ! CALCULATE SOIL SURFACE TEMPERATURE TODAY USING COSINE FUNCTION
      X4=.5*AMPX
      X8=(IDA-200)/PIT
      DST0(ISA)=AVT+X4*COS(X8)
      ! CALCULATE TEMP / RADIATION FACTOR
      X1=(TMX(IRF(ISA))-TMN(IRF(ISA)))*SQRT(.03*ST0X)
      ! DECIDE BETWEEN COOL AND WARM SEASON
      IF(DST0(ISA)<AVT)THEN          
          ! COOL SEASON
          BCV(ISA)=SNOF
          ! CALCULATE SURFACE TEMP DSTC USING TX & TMN
          DSTC=.5*(TX+TMN(IRF(ISA)))
          ! ADJUST DSTC USING NORMAL TEMP FOR TODAY 
          IF(TX<0.)THEN
              DSTC=.5*(DSTC+X2)
          ELSE
              DSTC=MIN(DSTC,X2)
          END IF
          ! ADJUST FOR SOLAR RADIATION
          IF(SRAD(IRF(ISA))>10.)THEN
              F=ST0X/(ST0X+EXP(SCRP(19,1)-SCRP(19,2)*ST0X))
              DSTC=DSTC+X1*F
          END IF
          ! ADJUST FOR SNOW COVER
          DSTC=DSTC*(1.-SNOF)
      ELSE
          ! WARM SEASON
          DSTC=TX+(1.-BCV(ISA))*X1
          DSTC=MAX(DSTC,X2)
      END IF    
      ! CAALCULATE 5 DAY MOVING AVE DSTC
      SMTC(ISA)=SMTC(ISA)-TTAC(5,ISA)
      DO K=5,2,-1
          TTAC(K,ISA)=TTAC(K-1,ISA)
      END DO
      TTAC(1,ISA)=DSTC
      SMTC(ISA)=SMTC(ISA)+DSTC
      DSTC=SMTC(ISA)/5.
      XX=0.
      ! CALCULATE DIFFERENCE BETWEEN DSTC & COSINE TEMP (DST0)
      X1=DSTC-DST0(ISA)
      ! ADJUST DIFFERENCE USING EXPONENT
      IF(X1>0.)THEN
          SGN=1.
          X2=X1**.6
      ELSE
          SGN=-1.
          X2=ABS(X1)**.6
      END IF
      X1=X2*SGN
      ! CALCULATE STMP FOR EACH LAYER USING COSINE ADJUSTED BY DSTC
      DO J=1,NBSL(ISA)
          ISL=LID(J,ISA)
          ZD=(XX+Z(ISL,ISA))/ZZ
          X5=EXP(-2.*ZD)
          STMP(ISL,ISA)=AVT+(X4*COS(X8-ZD)+X1)*X5
          IF(STMP(ISL,ISA)<0..AND.Z(ISL,ISA)>FRSTMX(IY,ISA))FRSTMX(IY,ISA)=Z(ISL,ISA)
          XX=Z(ISL,ISA)
      END DO
      IF(KFL(10)>0)WRITE(KW(10),107)IYR,MO,KDA,TMX(IRF(ISA)),TMN(IRF(ISA)),SRAD(IRF(ISA)),X2,&
      DSTC,(STMP(LID(K,ISA),ISA),K=1,NBSL(ISA)),SNO(ISA),CV(ISA),BCV(ISA),DD,RZSW(ISA) 
      RETURN
  107 FORMAT(1X,3I4,50F10.3)                                                    
      END